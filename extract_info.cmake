cmake_minimum_required(VERSION 3.20)

# https://stackoverflow.com/questions/60211516/programmatically-get-all-targets-in-a-cmake-project
function(get_all_targets _result _dir)
    get_property(_subdirs DIRECTORY "${_dir}" PROPERTY SUBDIRECTORIES)
    foreach(_subdir IN LISTS _subdirs)
        get_all_targets(${_result} "${_subdir}")
    endforeach()

    get_directory_property(_sub_targets DIRECTORY "${_dir}" BUILDSYSTEM_TARGETS)
    set(${_result} ${${_result}} ${_sub_targets} PARENT_SCOPE)
endfunction()

function(get_target_sources SOURCES TARGET)
    get_target_property(TARGET_DIR ${TARGET} SOURCE_DIR)
    get_target_property(TARGET_SOURCES_RAW ${TARGET} SOURCES)
    set(TARGET_SOURCES "")
    foreach(TARGET_SOURCE ${TARGET_SOURCES_RAW})
        # Complex projects may contain generator expressions so we handle that here since there is no way to evaluate that expression
        if(TARGET_SOURCE MATCHES "\\$<TARGET_OBJECTS:([^>]+)>")
            set(SOURCE_TARGET ${CMAKE_MATCH_1})
            if(TARGET "${SOURCE_TARGET}")
                get_target_sources(SOURCE_SOURCES ${SOURCE_TARGET})
                if(SOURCE_SOURCES)
                    list(APPEND TARGET_SOURCES ${SOURCE_SOURCES})
                endif()
            endif()
        else()
            set(TARGET_SOURCE "${TARGET_DIR}/${TARGET_SOURCE}")
            list(APPEND TARGET_SOURCES ${TARGET_SOURCE})
        endif()
    endforeach()
    set(${SOURCES} ${TARGET_SOURCES} PARENT_SCOPE)
endfunction()

function(extract_info)
    message(STATUS "Detecting targets ...")
    get_all_targets(ALL_TARGETS ${PROJECT_SOURCE_DIR})
    foreach(TARGET ${ALL_TARGETS})
        get_target_property(TARGET_TYPE ${TARGET} TYPE)
        get_target_property(TARGET_DIR ${TARGET} SOURCE_DIR)
        get_target_property(TARGET_LINK_LIBRARIES ${TARGET} LINK_LIBRARIES)

        message(STATUS "  Found ${TARGET_TYPE} ${TARGET}")

        # Recursively get target sources and and shared library/object sources
        get_target_sources(TARGET_SOURCES ${TARGET})
        foreach(LINK_TARGET IN LISTS TARGET_LINK_LIBRARIES)
            if(TARGET ${LINK_TARGET})
                get_target_sources(LINK_SOURCES ${LINK_TARGET})
                list(APPEND TARGET_SOURCES ${LINK_SOURCES})
            endif()
        endforeach()
        list(JOIN TARGET_SOURCES "\n" TARGET_SOURCES)

        if(${TARGET_TYPE} STREQUAL "OBJECT_LIBRARY")
            set(TARGET_NAME ${TARGET})
        else()
            set(TARGET_NAME $<TARGET_FILE_NAME:${TARGET}>)
        endif()
        file(GENERATE OUTPUT "${TARGET_NAME}.type" CONTENT "${TARGET_TYPE}")
        file(GENERATE OUTPUT "${TARGET_NAME}.dir" CONTENT "${TARGET_DIR}")
        file(GENERATE OUTPUT "${TARGET_NAME}.sources" CONTENT "${TARGET_SOURCES}")
    endforeach()
endfunction()

cmake_language(DEFER CALL extract_info)
